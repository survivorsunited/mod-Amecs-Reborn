plugins {
    id("fabric-loom") version "1.14.1"
    id("maven-publish")
}

group = property("maven_group")!!
version = property("mod_version") as String + "+mc" + property("minecraft_version") as String
val baseName = property("archives_base_name") as String


repositories {
    mavenCentral()
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = uri("https://api.modrinth.com/maven")
            }
        }
        filter {
            includeGroup("maven.modrinth")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Terraformers"
                url = uri("https://maven.terraformersmc.com/")
            }
        }
        filter {
            includeGroup("com.terraformersmc")
        }
    }
}

base {
    archivesName.set(property("mod_name") as String)
}

loom {
    accessWidenerPath = rootProject.file("src/main/resources/amecs.accesswidener")
}

sourceSets.named("main") {
    java {
        // The Controlling compat mixins target the pre-1.21.11 Controlling keybind screen API.
        // Keep the 1.21.11 branch buildable by excluding them until that integration is ported.
        exclude("de/siphalor/amecs/mixin/compat/**")
    }
}


dependencies {
    //to change the versions see the gradle.properties file
    minecraft("com.mojang:minecraft:${property("minecraft_version")}")
    mappings("net.fabricmc:yarn:${property("yarn_mappings")}:v2")
    modImplementation("net.fabricmc:fabric-loader:${property("loader_version")}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${property("fabric_version")}")
    include(implementation("com.moulberry:mixinconstraints:${(property("mcon_version"))}")!!)
    include(modImplementation("wtf.cheeze:platformlanguageloader-fabric:${property("pll_version")}")!!)

    modImplementation("com.terraformersmc:modmenu:${property("modmenu_version")}")
}


tasks.processResources {
    inputs.property("version", project.version)
    inputs.property("support_range", project.property("support_range") as String)

    filesMatching("fabric.mod.json") {
        expand(mapOf("version" to project.version, "support_range" to project.property("support_range") as String))

    }
}

tasks.withType<JavaCompile>().configureEach {
    options.release.set(21)
}


java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}


tasks.named<Jar>("jar") {

    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}
